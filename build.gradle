import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url "https://oss.sonatype.org/service/local/repositories/releases/content" }
  }
}

plugins {
  id "java"
  id 'net.researchgate.release' version '3.0.2'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'org.graalvm.buildtools.native' version '0.10.2'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://oss.sonatype.org/service/local/repositories/releases/content" }
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

sourceSets {
  stressTest {
    java {
      srcDir 'src/stress-test/java'
    }
    resources {
      srcDir 'src/stress-test/resources'
    }
    compileClasspath += sourceSets.test.runtimeClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }

}

dependencies {

  compileOnly('org.projectlombok:lombok:1.18.+')
  annotationProcessor('org.projectlombok:lombok:1.18.+')

  compileOnly('com.mageddo.nativeimage:reflection-config-generator:2.4.4')
  annotationProcessor('com.mageddo.nativeimage:reflection-config-generator:2.4.4')

  testCompileOnly('com.mageddo.nativeimage:reflection-config-generator:2.4.4')
  testAnnotationProcessor('com.mageddo.nativeimage:reflection-config-generator:2.4.4')

  annotationProcessor("com.google.dagger:dagger-compiler:2.45")
  implementation("com.google.dagger:dagger:2.45")

  implementation('org.graalvm.sdk:nativeimage:24.0.1')

  implementation('jakarta.enterprise:jakarta.enterprise.cdi-api:2.0.2')
  implementation('jakarta.ws.rs:jakarta.ws.rs-api:2.1.6')
  implementation('com.mageddo.commons:commons-lang:0.1.21')
  implementation('org.apache.commons:commons-exec:1.3')
  implementation("org.apache.commons:commons-text:1.14.0")

  implementation('ch.qos.logback:logback-classic:1.5.6')
  implementation('net.java.dev.jna:jna:5.13.0')
  implementation('dnsjava:dnsjava:3.5.2')
  implementation('org.apache.commons:commons-compress:1.22')
  implementation('org.rauschig:jarchivelib:1.2.0')
  implementation('dev.failsafe:failsafe:3.3.1')
  implementation("io.github.resilience4j:resilience4j-circuitbreaker:2.2.0")

  implementation('com.github.docker-java:docker-java-core:3.7.0')
  implementation('com.github.docker-java:docker-java-transport-httpclient5:3.7.0')

  implementation('info.picocli:picocli:4.7.6')
  implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3')
  implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.3')

  implementation('com.github.ben-manes.caffeine:caffeine:3.1.8')

  testAnnotationProcessor("com.google.dagger:dagger-compiler:2.45")

  testCompileOnly('org.projectlombok:lombok:1.18.+')
  testAnnotationProcessor('org.projectlombok:lombok:1.18.+')

  stressTestCompileOnly('org.projectlombok:lombok:1.18.+')
  stressTestAnnotationProcessor('org.projectlombok:lombok:1.18.+')

  testImplementation("org.junit.jupiter:junit-jupiter:5.10.+")
  testImplementation('org.mockito:mockito-junit-jupiter:5.12.+')
  testImplementation('org.hamcrest:hamcrest:3.0')
  testImplementation('io.rest-assured:rest-assured:5.5.0')
  testImplementation('org.reflections:reflections:0.10.2')

}

test {
  useJUnitPlatform()
  exclude "**/*CompTest.class", "**/*IntTest.class"
  testLogging {
    events "passed", "skipped", "failed"
  }
  testLogging {
    showStandardStreams = true
  }
}

def compTest = tasks.register("compTest", Test) {
  useJUnitPlatform()
  include "**/*CompTest.class"
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
  }
  testLogging {
    showStandardStreams = true
  }
}

tasks.register("allTests", Test) {
  dependsOn test, compTest, nativeIntTest
}

def intTest = tasks.register("intTest", Test) {

  dependsOn shadowJar
  useJUnitPlatform()
  include "**/*IntTest.class"
  failFast = true

  testLogging {
    events "passed", "skipped", "failed"
  }

  testLogging {
    showStandardStreams = true
  }
}

task stressTest(type: Test) {

  testClassesDirs = sourceSets.stressTest.output.classesDirs
  classpath += sourceSets.stressTest.runtimeClasspath

  useJUnitPlatform()
  include "**/*Test.class"
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
  }
  testLogging {
    showStandardStreams = true
  }
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

processResources {
  filesMatching("**/application.properties") {
    expand version: java.util.Optional
      .ofNullable(System.getenv("BUILD_NUMBER"))
      .orElse(String.valueOf(project.version))
  }
}

def mainClassName = "com.mageddo.dnsproxyserver.App"
jar {
  manifest {
    attributes(
      "Main-Class": mainClassName
    )
  }
}

tasks.register('nativeImageJar', ShadowJar) {

  from sourceSets.main.output
  configurations = [project.configurations.runtimeClasspath]
  mergeServiceFiles()

  archiveBaseName = "native-image"

  manifest {
    attributes("Main-Class": mainClassName)
  }

  dependencies {
    exclude(dependency('org.graalvm.sdk:'))
  }

  exclude("/META-INF/*.SF")
  exclude("/META-INF/*.DSA")
  exclude("/META-INF/*.RSA")
}

shadowJar {
  mergeServiceFiles()
}

graalvmNative {
  agent {
    defaultMode = "standard"
    metadataCopy {
      inputTaskNames.add("compTest")
      outputDirectories.add("src/main/resources/META-INF/native-image/graal-auto-generated/")
      mergeWithExisting = true
    }
  }

  binaries {
    main {
      javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.matching("GraalVM Community")
      }

      imageName = project.name
      mainClass = mainClassName
      verbose = true
      fallback = false
      buildArgs.add('-J-Xmx5G')
      buildArgs.add("-O3")

    }

    configureEach {
      if(it.name == "intTest"){
        println "> configuring for: ${it.getName()}"
        buildArgs.add("-Ob")
      }
    }

  }

  registerTestBinary("intTest") {
    usingSourceSet(sourceSets.test)
    forTestTask(intTest)
  }
}

release {
  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch.set("")
    pushToRemote.set('') // disabling push to remote
  }
  failOnCommitNeeded = true
  failOnPublishNeeded = false
  failOnUnversionedFiles = true
  buildTasks = []

}

def dontCreateTagsAsTheCdWill() {
  preTagCommit.enabled = false
  createReleaseTag.enabled = false
}

dontCreateTagsAsTheCdWill()

tasks.register("updateNewVersion") {
  doLast {
    def versionPattern = "\\d+\\.\\d+\\.\\d+(?:-beta|-snapshot|-alpha)?"
    updateVersion("README.md", "/${versionPattern}/", "/${version}/")
    updateVersion("README.md", "${versionPattern}.tgz", "${version}.tgz")
  }
}
tasks["updateVersion"].finalizedBy("updateNewVersion")

def updateVersion(fileName, pattern, version) {
  println("> file=${fileName}, pattern=${pattern}, to=${version}")
  def f = file("${rootDir}/$fileName")
  def text = f.text
  f.withWriter { w ->
    w << text.replaceAll(pattern, version)
  }
}

task setVersionOnActions() {
  doLast {
    appendAppVersion(System.getenv("GITHUB_ENV"))
    appendAppVersion(System.getenv("GITHUB_OUTPUT"))
  }
}

void appendAppVersion(String f) {
  println("> writing ${project.version} to $f")
  file(f).append("APP_VERSION=${project.version}\n")
}
